/*
*    Copyright (C) 2015 Haruki Hasegawa
*
*    Licensed under the Apache License, Version 2.0 (the "License");
*    you may not use this file except in compliance with the License.
*    You may obtain a copy of the License at
*
*        http://www.apache.org/licenses/LICENSE-2.0
*
*    Unless required by applicable law or agreed to in writing, software
*    distributed under the License is distributed on an "AS IS" BASIS,
*    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
*    See the License for the specific language governing permissions and
*    limitations under the License.
*/

apply plugin: 'com.android.library'

ext {
    // required for 'android-maven-publish.gradle'
    mavenPublishDestDir = new File(rootDir, "repository").absolutePath
    mavenPublishDataFile = file('./library-data.properties').absolutePath
    mavenPublishSigningSetting =  file('../signing/library-maven-publish-signing.properties').absolutePath
}

// Common configurations
apply from: '../common/android-set-common-properties.gradle'

android {
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23


        def dataProps = new Properties()
        dataProps.load(project.file(project.mavenPublishDataFile).newDataInputStream())

        //noinspection GroovyAssignabilityCheck
        versionCode dataProps.VERSION_CODE.toInteger()
        versionName dataProps.VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            consumerProguardFiles 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

// NOTE: this line should be place at end of the file
apply from: '../common/android-maven-publish.gradle'
