/*
*    Copyright (C) 2015 Haruki Hasegawa
*
*    Licensed under the Apache License, Version 2.0 (the "License");
*    you may not use this file except in compliance with the License.
*    You may obtain a copy of the License at
*
*        http://www.apache.org/licenses/LICENSE-2.0
*
*    Unless required by applicable law or agreed to in writing, software
*    distributed under the License is distributed on an "AS IS" BASIS,
*    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
*    See the License for the specific language governing permissions and
*    limitations under the License.
*/

apply plugin: 'com.android.library'

ext {
    // required for 'android-maven-publish.gradle'
    mavenPublishDestDir = new File(rootDir, "repository").absolutePath
    mavenPublishDataFile = file('./library-data.properties').absolutePath
    mavenPublishSigningSetting =  file('../signing/library-maven-publish-signing.properties').absolutePath

    useDoclava = true
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        def dataProps = new Properties()
        dataProps.load(project.file(project.mavenPublishDataFile).newDataInputStream())

        //noinspection GroovyAssignabilityCheck
        versionCode dataProps.VERSION_CODE.toInteger()
        versionName dataProps.VERSION_NAME

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            consumerProguardFiles 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation "androidx.recyclerview:recyclerview:${rootProject.ext.recyclerViewVersion}"

    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:rules:1.1.0'
    testImplementation 'androidx.test:runner:1.1.0'
    testImplementation ('androidx.test.ext:junit:1.0.0') {
        exclude group: 'org.junit', module: 'junit' // is this a bug???
    }
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.robolectric:robolectric:4.0.2'
 
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation ('androidx.test.ext:junit:1.0.0') {
        exclude group: 'org.junit', module: 'junit' // is this a bug???
    }
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

// NOTE: this line should be place at end of the file
apply from: '../common/android-maven-publish.gradle'
