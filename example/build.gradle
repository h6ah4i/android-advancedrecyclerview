/*
 *    Copyright (C) 2015 Haruki Hasegawa
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'com.android.application'

apply from: '../common/android-signing-utils.gradle'

android {
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.h6ah4i.android.example.advrecyclerview"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode 30
        versionName "1.0.0"
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release
    }

    flavorDimensions = ["minSdk"]

    productFlavors {
        // for saving build time during debugging
        lollipop {
            dimension "minSdk"
            minSdk 21
        }
        ics {
            dimension "minSdk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// load signing config from external properties file
if (loadSigningConfigFromPropertiesFile(
    project, android.signingConfigs.release, '../signing/example-signing.properties')) {
        android.buildTypes.release.signingConfig android.signingConfigs.release
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':library')
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.h6ah4i.android.tablayouthelper:tablayouthelper:1.0.0'
}
