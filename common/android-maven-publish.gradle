/*
 *    Copyright (C) 2015 Haruki Hasegawa
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

// [Usage]]
// - publish to build directory
//   ./gradlew library:publishReleasePublicationToBuildDir
// - publish to sonatype maven repository
//   ./gradlew library:publishReleasePublicationToSonatypeRepository
// - publish to local maven repository
//   ./gradlew library:publishToMavenLocal

apply plugin: 'maven-publish'
apply plugin: 'signing'

// Declare these properties in project's extension
// - ext.mavenPublishDestDir = [path to destination directory]
// - ext.mavenPublishDataFile = [path to data properties file]

def dataProps = new Properties()
dataProps.load(project.file(project.mavenPublishDataFile).newDataInputStream())
def libraryVersionName = dataProps.VERSION_NAME

android {
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.h6ah4i.android.widget.advrecyclerview'
                artifactId = 'advrecyclerview'
                version = libraryVersionName

                pom {
                    name = 'Advanced RecyclerView'
                    description = 'RecyclerView extension library which provides advanced features. (ex. swiping, drag and drop sorting, expandable items, etc...)'
                    url = 'https://github.com/h6ah4i/android-advancedrecyclerview'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'h6ah4i'
                            name = 'Haruki Hasegawa'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/h6ah4i/android-advancedrecyclerview.git'
                        developerConnection = 'scm:git:https://github.com/h6ah4i/android-advancedrecyclerview.git'
                        url = 'https://github.com/h6ah4i/android-advancedrecyclerview'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "BuildDir"
                url = project.mavenPublishDestDir
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.release
    }
}
